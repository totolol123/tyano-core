AC_PREREQ([2.69])
AC_INIT([The Forgotten Tyano Server],[1.0],[],[tfts])
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_SRCDIR([sources/server.h])
AM_INIT_AUTOMAKE([foreign -Wall -Werror])
AC_LANG([C++])


# unset defaults
CXXFLAGS=""


# read arguments 
AC_ARG_ENABLE([optimizations],
	AS_HELP_STRING([--disable-optimizations], [disable compile- and link-time optimizations]))
	
AC_ARG_ENABLE([profiling],
	AS_HELP_STRING([--enable-profiling], [enable profiler support]))
	
AC_ARG_ENABLE([debugging-symbols],
	AS_HELP_STRING([--disable-debugging-symbols], [strip debugging symbols from server for smaller file size]))


# checks generated by autoscan
AC_PROG_CXX
AC_CHECK_HEADERS([arpa/inet.h netdb.h netinet/in.h stddef.h stdint.h stdlib.h sys/socket.h sys/timeb.h unistd.h])
AC_C_INLINE
AC_TYPE_INT16_T
AC_TYPE_INT32_T
AC_TYPE_INT64_T
AC_TYPE_INT8_T
AC_TYPE_SIZE_T
AC_TYPE_UINT16_T
AC_TYPE_UINT32_T
AC_TYPE_UINT64_T
AC_TYPE_UINT8_T
AC_FUNC_ERROR_AT_LINE
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_CHECK_FUNCS([atexit floor ftime gethostbyname gethostname memset pow socket sqrt strcasecmp strncasecmp])


# C++11
AX_CXX_COMPILE_STDCXX_11([ext])


# Boost DateTime, Filesystem, Regex, System, Thread
AC_CHECK_LIB([boost_date_time-mt], [main])
AC_CHECK_LIB([boost_filesystem-mt], [main])
AC_CHECK_LIB([boost_regex-mt], [main])
AC_CHECK_LIB([boost_system-mt], [main])
AC_CHECK_LIB([boost_thread-mt], [main])


# gmp
AC_CHECK_HEADERS([gmp.h])
AC_CHECK_LIB([gmp], [__gmpz_init2])


# log4cxx >= 0.10
PKG_CHECK_MODULES([LOG4CXX], [liblog4cxx >= 0.10], [LIBS="$LOG4CXX_LIBS $LIBS"], [
	AC_CHECK_HEADERS([log4cxx/log4cxx.h])
	AC_CHECK_LIB([log4cxx], [main])
])


# LUA >= 5.1
PKG_CHECK_MODULES([LUA], [lua5.1], [LIBS="$LUA_LIBS $LIBS"], [
	PKG_CHECK_MODULES([LUA], [lua >= 5.1], [LIBS="$LUA_LIBS $LIBS"], [
		AC_CHECK_HEADERS([lua.hpp])
		AC_CHECK_LIB([lua5.1], [main])
	])
])


# MySQL
AC_CHECK_HEADERS([mysql/mysql.h])
AC_CHECK_LIB([mysqlclient], [main])


# XML >= 2.6.5
AM_PATH_XML2([2.6.5], [LIBS="$XML_LIBS $LIBS"], AC_MSG_ERROR([*** Cannot find working libxml2.]))


# apply arguments
AS_IF([test "x${enable_optimizations:=yes}" = "xyes"], [CXXFLAGS="$CXXFLAGS -O3"], [CXXFLAGS="$CXXFLAGS -O0"])
AS_IF([test "x${enable_profiling:=no}" = "xyes"], [CXXFLAGS="$CXXFLAGS -pg"])
AS_IF([test "x${enable_debugging_symbols:=yes}" = "xyes"], [CXXFLAGS="$CXXFLAGS -g"], [CXXFLAGS="$CXXFLAGS -s"])


AC_CONFIG_HEADERS([config.h])
AC_CONFIG_FILES([Makefile])
AC_OUTPUT


echo
echo $PACKAGE_STRING
echo
echo Debugging Symbols ... $enable_debugging_symbols
echo Optimizations ....... $enable_optimizations
echo Profiling ........... $enable_profiling
echo
echo Configuration complete!
echo
